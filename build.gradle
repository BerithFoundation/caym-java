buildscript {
    ext.web3jVersion = '4.5.5'
    ext.slf4jVersion = '1.7.25'

    // test dependencies
    ext.junitVersion = '4.12'
    ext.mockitoVersion = '1.10.19'
}

plugins {
    id 'java'
    id 'idea'
    id 'maven'
}

allprojects {
    description 'caym-java project'
    version '0.1.1'
    group 'berith.caym'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    javadoc {
        options.encoding = 'UTF-8'
    }
}

subprojects {
    apply plugin: 'java'

    dependencies {
        testCompile "junit:junit:$junitVersion",
                "org.mockito:mockito-core:$mockitoVersion",
                "ch.qos.logback:logback-core:1.2.3",
                "ch.qos.logback:logback-classic:1.2.3"
    }

    repositories {
        mavenCentral()
    }
}

configure(subprojects.findAll { it.name != 'integration-test' }) {
    apply plugin: 'maven'

    // Deploy
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task testJar(type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    ext {
        repoUrl = project.hasProperty('repositoryUrl') ? project.property('repositoryUrl') : System.getenv('REPOSITORY_URL')
        mavenuser = project.hasProperty('mavenUser') ? project.property('mavenUser') : System.getenv('MAVEN_USER')
        mavenpassword = project.hasProperty('mavenPassword') ? project.property('mavenPassword') : System.getenv('MAVEN_PASSWORD')
        isSnapshotVersion = project.version.endsWith("-SNAPSHOT")
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: repoUrl) {
                    authentication(userName: mavenuser, password: mavenpassword)
                }
            }
        }
    }

    task release {
        dependsOn 'build'
        dependsOn 'uploadArchives'

        tasks.findByName('uploadArchives').mustRunAfter 'build'
    }
}
